//----------------------------------------------------------------------
// mjParser0.jj
//
// (For CS321 Fall 2014 - Will Hopper)
//----------------------------------------------------------------------
//
//
//options { DEBUG_PARSER=true; }  /* Show debugging info */

PARSER_BEGIN(mjParser)
import java.util.*;
import java.io.*;
import ast.*;

public class mjParser {
  public static void main(String [] args) {
    try {
      if (args.length == 1) {
        FileInputStream stream = new FileInputStream(args[0]);
        Ast.Program p = new mjParser(stream).Program();
        stream.close();
        System.out.print(p);
      } else {
        System.out.println("Need a file name as command-line argument.");
      }
    } catch (TokenMgrError e) {
      System.err.println(e);
    } catch (Exception e) {
      System.err.println(e);
    }
  }
}
PARSER_END(mjParser)

//
// LEXER SECTION ---------------------------------------------------------------
//

SKIP : /* White space */
{
  " " | "\t" | "\n" | "\r" | "\f"
}

SKIP : /* Comments -- multi-line form and error detection not included */
{
  <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
| "/*" : MULTILINECOMMENT // Here we enter multiline comment state
}

// Allows us to catch unterminated comments
<MULTILINECOMMENT> MORE:
{
  <~[]>
}

// Rules for inside of multiline comment state
<MULTILINECOMMENT> SKIP :
{
  "*/" : DEFAULT // Back to the default state
}

TOKEN : /* Keywords */
{
  "class" | "extends" | "static" | "public" | "void" | "int" | "boolean"
| "new" | "this" | "if" | "else" | "while" | "return" | "main" | "String"
| "true" | "false" | "System" | "out" | "println" 
}

TOKEN : /* Internal tokens */
{
  <#DIGIT:  ["0"-"9"]>
| <#LETTER: (["A"-"Z"]|["a"-"z"])>
}

TOKEN : /* Integer literals */
{
  <INTLIT: (<DIGIT>)+>
    {
      /* Detect integer overflow */
      try {
        Integer.parseInt(matchedToken.image);
      } catch(NumberFormatException e) {
        throw new TokenMgrError("Lexical error: Integer overflow: line "
          + matchedToken.beginLine + ", column " + matchedToken.beginColumn
          + ", lexeme: " + matchedToken.image, 0); /* Detect integer overflow */
      }
    }
}

TOKEN : /* String literals  */
{
  <STRLIT: ("\"" (~["\"","\n"])* "\"")>
| <UNTERM_STRLIT: ("\"" (~["\"", "\n"])*)>
  { if (true) throw new TokenMgrError("Lexical error: Unterminated string: line "
        + matchedToken.beginLine + ", column " + matchedToken.beginColumn
        + ", lexeme: " + matchedToken.image, 0); } /* Detect unterm. strings */
}

TOKEN : /* Identifiers */
{
  <ID: <LETTER> (<LETTER>|<DIGIT>)*>
}

TOKEN : /* Operators and delimiters */
{
   "+" | "-" | "*" | "/" | "&&" | "||" | "!"
|  "==" | "!=" | "<" | "<=" | ">" | ">="
|  "=" | ";" | "," | "." | "(" | ")" | "[" | "]" | "{" | "}"
}

//
// PARSER SECTION ---------------------------------------------------------------
//

// Program -> {ClassDecl}
//
Ast.Program Program():
{ List<Ast.ClassDecl> cl = new ArrayList<Ast.ClassDecl>();
  Ast.ClassDecl c; }
{
  ( c=ClassDecl() {cl.add(c);} )* <EOF>
  { return new Ast.Program(cl); }
}

/*
 * ClassDecl -> "class" <ID> ["extends" <ID>] "{" {VarDecl} {MethodDecl} "}"
 * (String classname, String parentclass, list of VarDecls, list of MethodDecls)
*/
Ast.ClassDecl ClassDecl():
{ Token tkn;
  String cn;
  String pc="";
  List<Ast.VarDecl> vdl = new ArrayList<Ast.VarDecl>();
  List<Ast.MethodDecl> mdl = new ArrayList<Ast.MethodDecl>();
  Ast.VarDecl v;
  Ast.MethodDecl m; }
{
  "class" tkn=<ID> { cn=new String(tkn.image); } ["extends" tkn=<ID> {pc=new String(tkn.image);}] "{"
          ( v=VarDecl() {vdl.add(v);} )*
          ( m=MethodDecl() {mdl.add(m);} )* "}"

  { return new Ast.ClassDecl(cn, pc, vdl, mdl); }

}

/* Original:
 * MethodDecl -> "public" ExtType <ID> "(" [Param {"," Param}] ")"
 *                 "{" {VarDecl} {Stmt} "}"
 *            |  "public" "static" "void" "main" "(" "String" "[" "]" <ID> ")"
 *                 "{" {VarDecl} {Stmt} "}"
 * (Type returntype, String name, list of params, list of VarDecls, list of Stmts)
*/
Ast.MethodDecl MethodDecl():
{
  Token tkn;
  Ast.Type t=null;
  Ast.Param p;
  Ast.VarDecl v;
  Ast.Stmt s;
  List<Ast.Param> plist = new ArrayList<Ast.Param>();
  List<Ast.VarDecl> vlist = new ArrayList<Ast.VarDecl>();
  List<Ast.Stmt> slist = new ArrayList<Ast.Stmt>();
}
{
"public" ( t=ExtType() tkn=<ID> "(" [p=Param() {plist.add(p);} ("," p=Param() {plist.add(p);})*] ")"
         | "static" "void" tkn="main" "(" "String" "[" "]" <ID> ")" )
         "{" (LOOKAHEAD(2)v=VarDecl() {vlist.add(v);})* (s=Stmt() {slist.add(s);})* "}"

          { return new Ast.MethodDecl(t, tkn.image, plist, vlist, slist); }

}

// Param -> Type <ID>
// (Type type, String name)
Ast.Param Param():
{
  Token tkn;
  Ast.Type t;
}
{
  t=Type() tkn=<ID>
  { return new Ast.Param(t, tkn.image); }
}

// VarDecl -> Type <ID> ["=" InitExpr] ";"
// (Type type, String name, Exp initExpr (possibly null))
Ast.VarDecl VarDecl():
{
  Token tkn;
  Ast.Type t;
  Ast.Exp initExp=null;
}
{
  t=Type() tkn=<ID> ["=" initExp=InitExpr()] ";"
  { return new Ast.VarDecl(t, tkn.image, initExp); }
}

// ExtType -> Type | "void"
//
Ast.Type ExtType(): {Ast.Type t;}
{
  t=Type() { return t; }
| "void"   { return null; }
}

// Type -> BasicType
//      |  BasicType "[" "]"
//      |  <ID>
//
Ast.Type Type(): {Ast.Type t; Token tkn; boolean isArray=false;}
{
    t=BasicType() ["[" "]" {isArray = true;}]
      { if(isArray == true) {
          return new Ast.ArrayType(t);
        } else {
          return t;
        }
      }

  | tkn=<ID>  { return new Ast.ObjType(tkn.image); }
}

// BasicType -> "int" | "boolean"
//
Ast.Type BasicType(): {}
{
  "int"     { return new Ast.IntType();  }
| "boolean" { return new Ast.BoolType(); }
}

// Stmt -> "{" {Stmt} "}"
//      |  ExtId "(" [Args] ")" ";"
//      |  Lvalue "=" InitExpr ";"
//      |  "if" "(" Expr ")" Stmt ["else" Stmt]
//      |  "while" "(" Expr ")" Stmt
//      |  "System" "." "out" "." "println" "(" [PrintArg] ")" ";"
//      |  "return" [Expr] ";"
//
Ast.Stmt Stmt():
{
  Ast.Type t;
  List<Ast.Param> plist = new ArrayList<Ast.Param>();
  Ast.Param p;
  List<Ast.VarDecl> vlist = new ArrayList<Ast.VarDecl>();
  Ast.VarDecl v;
  List<Ast.Stmt> slist = new ArrayList<Ast.Stmt>();
  Ast.Stmt s1;
  Ast.Stmt s2=null;
  Ast.Stmt finalStmt;

  Ast.Exp eLeft;
  Ast.Exp e1=null;
  Ast.Exp e2=null;
  Ast.Exp e3=null;
  List<Ast.Exp> elist = new ArrayList<Ast.Exp>();
  Ast.PrArg pr=null;
  String str = null;
}
{
  "{" (s1=Stmt() {slist.add(s1);})* "}" { return new Ast.Block(slist); }

| eLeft=ExtId() (
    ["["e1=Expr()"]"] "=" e2=InitExpr() ";" // Possible: eLeft[10] = x;, eLeft = x;
      {
        if(e1 != null) {  // eLeft[10] = x; Assignment
          e3 = new Ast.ArrayElm(eLeft, e1);
          return new Ast.Assign(e3, e2);
        } else {         // eLeft = x; Assignment
          return new Ast.Assign(eLeft, e2);
        }
      }
    | "(" [elist=Args()] ")" ";"          // Possible: eLeft();, eLeft(ArgA, ArgB);
      {
        if(eLeft instanceof Ast.Id) {
          e3  = new Ast.This();    // The object is This
          str = ((Ast.Id)eLeft).nm; // Get method name
        } else if(eLeft instanceof Ast.Field) {
          e3  = ((Ast.Field)eLeft).obj; // Get object name
          str = ((Ast.Field)eLeft).nm;  // Get method name
        }
        return new Ast.CallStmt(e3, str, elist);
      }
    )
| "if" "(" e1=Expr() ")" s1=Stmt() ["else" s2=Stmt()] { return new Ast.If(e1, s1, s2);}
| "while" "(" e1=Expr() ")" s1=Stmt()                 { return new Ast.While(e1, s1); }
| "System" "." "out" "." "println" "(" [ pr=PrintArg() ] ")" ";" { return new Ast.Print(pr); }
| "return" [e1=Expr()] ";"                            { return new Ast.Return(e1); }
}

// Args -> Expr {"," Expr}
//
List<Ast.Exp> Args():
{
  Ast.Exp e1;
  Ast.Exp e2;
  List<Ast.Exp> elist = new ArrayList<Ast.Exp>();
}
{
  e1=Expr() {elist.add(e1);} ("," e2=Expr() {elist.add(e2);})* { return elist; }
}

// PrintArg -> Expr | <STRLIT>
//
Ast.PrArg PrintArg():
{
  Token tkn;
  Ast.Exp e;
}
{
 e=Expr()       { return e; }
 | tkn=<STRLIT> { return new Ast.StrLit(tkn.image.replace("\"", "")); }
}

// InitExpr -> "new" BasicType "[" <INTLIT> "]"
//          |  "new" <ID> "(" ")"
//          |  Expr
//
 Ast.Exp InitExpr():
{ Token tkn;
  Ast.Type t=null;
  Ast.Exp e;
}
{
  "new" ( t=BasicType() "[" tkn=<INTLIT> "]" | tkn=<ID> "(" ")" )
    { if(t != null) { // This is an array declaration
        return new Ast.NewArray(t, Integer.parseInt(tkn.image));
      } else {        // This is an object declaration
        return new Ast.NewObj(tkn.image);
      }
    }
| e=Expr() { return e; }
}

Ast.Exp Expr(): {Ast.Exp e1, e2=null; Ast.BOP op; Token tkn=null;}
{
  e1=AndExpr() (op=BinopA() e2=AndExpr() { e1 = new Ast.Binop(op, e1, e2); })*
  { return e1; }
}

Ast.Exp AndExpr(): {Ast.Exp e1, e2=null; Ast.BOP op; Token tkn=null;}
{
  e1=RelExpr() (op=BinopB() e2=RelExpr() { e1 = new Ast.Binop(op, e1, e2); })*
  { return e1; }
}

Ast.Exp RelExpr(): {Ast.Exp e1, e2=null; Ast.BOP op; Token tkn=null;}
{
  e1=ArithExpr() (op=BinopC() e2=ArithExpr() { e1 = new Ast.Binop(op, e1, e2); })*
  { return e1; }
}

Ast.Exp ArithExpr(): {Ast.Exp e1, e2=null; Ast.BOP op; Token tkn=null;}
{
  e1=Term() (op=BinopD() e2=Term() { e1 = new Ast.Binop(op, e1, e2); })*
  {return e1; }
}

Ast.Exp Term(): {Ast.Exp e1, e2=null; Ast.BOP op; Token tkn=null;}
{
  e1=Factor() (op=BinopE() e2=Factor() { e1 = new Ast.Binop(op, e1, e2); } )*
  { return e1; }
}

Ast.Exp Factor():
{
  Token tkn;
  Ast.Exp e1=null, e2=null, e3=null, eExpr=null;
  Ast.UOP u;
  List<Ast.Exp> elist = new ArrayList<Ast.Exp>();
  boolean b, isCall=false;
  int i;
  String str = null;
}
{
u=UnOp() e2=Factor()            { return new Ast.Unop(u, e2); }
| "(" e1=Expr() ")"             { return e1; }
| e1=ExtId()
    ("(" [elist=Args()] ")"
      {
        if(e1 instanceof Ast.Id) {
          e3 = new Ast.This();
          str = ((Ast.Id)e1).nm;
        } else if (e1 instanceof Ast.Field) {
          e3 = ((Ast.Field)e1).obj;
          str = ((Ast.Field)e1).nm;
        }
        return new Ast.Call(e3, str, elist);
      }
| ["[" e2=Expr() "]" {return new Ast.ArrayElm(e1,e2);} ])
{ return e1; }
| i=IntLit()                    { return new Ast.IntLit(i);  }
| b=BoolLit()                   { return new Ast.BoolLit(b); }

/*
    {
      if(e2 != null) {                // This is an array element
        return new Ast.ArrayElm(e1,e2);
      } else if(e1 instanceof Ast.Field) {
        return e1;
      } else if(e1 instanceof Ast.Id) {     // This is just an ID
        return e1;
      }
    }

*/

}

// ExtId -> ["this" "."] <ID> {"." <ID>}
//
Ast.Exp ExtId():
{
  Token tkn;
  Token tkn2 = null;
  Ast.Exp e1 = null;
  Ast.Exp e2 = null;
  List<Ast.Exp> fieldList = new ArrayList<Ast.Exp>();
  List<Token> IDList = new ArrayList<Token>();
}
{
  ["this" {e1=new Ast.This();} "."] tkn=<ID>
        {
          if (e1 == null) {
            e2 = new Ast.Id(tkn.image);
          } else {
            e2 = new Ast.Field(e1, tkn.image);
          }
        }

  ("." tkn2=<ID> {IDList.add(tkn2); System.out.println("adding: " + tkn2.image);})*
  { if(tkn2 != null) {
      if(IDList.size() == 1) {
        return new Ast.Field(e2, IDList.get(0).image);
      } else {
        Ast.Field outerField = new Ast.Field(e2, IDList.get(0).image);
        Ast.Field curField = outerField;
        for(int i=1; i<IDList.size(); ++i) {
          curField = new Ast.Field(curField, IDList.get(i).image);
        }
        return curField;
      }
    } else {
      return e2;
    }
  }
}

Ast.UOP UnOp(): {Token tkn;}
{
  tkn="-" { return Ast.UOP.NEG; }
| tkn="!" { return Ast.UOP.NOT; }
}

Ast.BOP BinopA(): {}
{
  "||" { return Ast.BOP.OR; }
}

Ast.BOP BinopB(): {}
{
 "&&" { return Ast.BOP.AND; }
}

Ast.BOP BinopC(): {}
{
 ">"   { return Ast.BOP.GT; }
| ">=" { return Ast.BOP.GE; }
| "<"  { return Ast.BOP.LT; }
| "<=" { return Ast.BOP.LE; }
| "==" { return Ast.BOP.EQ; }
| "!=" { return Ast.BOP.NE; }
}

Ast.BOP BinopD(): {}
{
 "+"   { return Ast.BOP.ADD; }
| "-"  { return Ast.BOP.SUB; }
}

Ast.BOP BinopE(): {}
{
 "*"   { return Ast.BOP.MUL; }
| "/"  { return Ast.BOP.DIV; }
}

int IntLit(): {Token tkn;}
{
  tkn=<INTLIT> { return Integer.parseInt(tkn.image); }
}

boolean BoolLit(): {Token tkn;}
{
  tkn="true"   { return true; }
| tkn="false"  { return false; }
}
